import java.util.regex.Matcher
import java.util.regex.Pattern

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        // Sdk
        min_sdk_version = '21'
        target_sdk_version = '29'

        // Core
        gradle_version = '3.5.2'
        kotlin_version = "1.3.50"
        google_play_services_version = "4.3.2"
        fabric_io_tools_version = "1.31.0"

        // Base
        appcompat_version = "1.1.0"
        legacy_support_version = "1.0.0"
        constraint_layout_version = '1.1.3'
        material_version = '1.2.0-alpha01'
        androidx_core_version = '1.2.0-beta01'
        androidx_fragment_version = '1.2.0-rc01'

        // Android's Architecture Components
        androidx_architecture_components_version = '2.2.0-rc01'

        // Databinding Library
        databinding_library_version = "3.2.0-alpha10"

        // Coroutines
        coroutines_version = "1.3.0"

        // Firebase Libraries
        firebase_core_version = '17.2.1'
        firebase_crashlytics_version = "2.10.1"
        firebase_auth_version = "19.1.0"

        // Google Play Services Auth
        play_services_auth_version = "17.0.0"

        // Retrofit
        retrofit_version = "2.6.2"

        // Dagger2
        dagger2_version = '2.25.2'

        // OkHTTP Logging interceptor
        okhttp_logging_interceptor_version = '4.2.2'

        // Testing
        test_runner_version = "1.2.0"
        test_espresso_core_version = "3.2.0"
        junit_version = "4.12"

        // Glide
        glide_version = '4.10.0'

        // Moxy-X
        moxy_x_version = "1.7.0"

        // CircleImageView
        circle_image_view_version = "3.0.1"
    }


    def versionPatchAndNumber = 0

    ext.getCurrentFlavor = { ->
        Gradle gradle = getGradle()
        String tskReqStr = gradle.getStartParameter().getTaskRequests().toString()

        Pattern pattern

        if (tskReqStr.contains("assemble"))
            pattern = Pattern.compile("assemble(\\w+)(Release|Debug)")
        else
            pattern = Pattern.compile("generate(\\w+)(Release|Debug)")

        Matcher matcher = pattern.matcher(tskReqStr)

        if (matcher.find())
            return matcher.group(1).toLowerCase()
        else {
            println "NO MATCH FOUND"
            return ""
        }
    }

    ext.versionPropsFile = file("app/version.properties")

    Properties versionProps = new Properties()

    versionProps.load(new FileInputStream(versionPropsFile))

    ext.versionMajor = 0
    ext.versionMinor = 0
    ext.versionPatch = versionProps["VERSION_PATCH"].toInteger() + versionPatchAndNumber
    ext.versionBuild = versionProps["VERSION_BUILD"].toInteger() + 1
    ext.versionNumber = versionProps["VERSION_NUMBER"].toInteger() + versionPatchAndNumber

    versionProps["VERSION_PATCH"] = versionPatch.toString()
    versionProps["VERSION_BUILD"] = versionBuild.toString()
    versionProps["VERSION_NUMBER"] = versionNumber.toString()

    versionProps.store(versionPropsFile.newWriter(), null)

    repositories {
        google()
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
